#!/usr/bin/env zsh
## jj auto bookmark
##
## Create a bookmark by normalizing the first line of the description of the change

usage() {
  echo "Usage: $(basename $0) [REV]"
  echo
  echo "Create a bookmark by normalizing the first line of the dscription of the change."
  echo "Then, prompt if you'd like to push the change to origin and potentially open"
  echo "a web browser to the create a PR page."
  echo
  echo "Default rev is @- (the parent of the current change)"
}

for arg in "$@"; do
  case "$arg" in
    -h|--help)
      usage
      exit 0
      ;;
  esac
done


local rev="$1"
if [[ -z $rev ]]; then
  rev="@-"
fi
local newname=$(jj log -r "$rev" --no-graph -n1 -T 'description.first_line()' \
  | sed -Ee 's/[^a-zA-Z1-9]+/-/g' \
  | sed -Ee 's/^-+|-+$//g' \
  | tr '[:upper:]' '[:lower:]' \
  ;
)
if [[ -z $newname ]]; then
  echo "bookmark at $rev has no description"
  return 1
fi
local mark="bwm/$newname"
jj bookmark create "$mark" -r "$rev"

local cmd=(jj git push --allow-new -b $mark)
local oldhist=""
local resethist=false
if [[ -n "${ZSH_AUTOSUGGEST_HISTORY_IGNORE:-}" ]]; then
  oldhist="${ZSH_AUTOSUGGEST_HISTORY_IGNORE}"
  resethist=true
fi

ZSH_AUTOSUGGEST_HISTORY_IGNORE=*

local doit
vared \
  -p "jjab: push new bookmark to forge? [y/N] " \
  -r "${cmd[*]}" \
  doit

if $resethist ; then
  ZSH_AUTOSUGGEST_HISTORY_IGNORE="$oldhist"
else
  unset ZSH_AUTOSUGGEST_HISTORY_IGNORE
fi

if [[ $doit =~ ^y ]]; then
  echo "> ${cmd[*]}"
  local outfile=$(mktemp --tmpdir jjab.out.XXXXXXXXXX)
  ${cmd[@]} > $outfile 2>&1
  cat $outfile
  local pr_url=$(grep -Eo 'https://github.com/.*pull/new[^ ]*' $outfile)
  rm "$outfile"
  if [[ -n "$pr_url" ]]; then
    python3 -m webbrowser "$pr_url"
  else
    echo "jjab: No PR URL found"
  fi
else
  echo "${cmd[*]}" | "$COPY_COMMAND"
  echo "jjab: bokmark push command is in your clipboard"
fi

