#!/usr/bin/env zsh
## jj auto bookmark
##
## Create a bookmark by normalizing the first line of the description of the change

usage() {
  echo "Usage: $(basename $0) [REV]"
  echo
  echo "Create a bookmark by normalizing the first line of the dscription of the change."
  echo "Then, prompt if you'd like to push the change to origin and potentially open"
  echo "a web browser to the create a PR page."
  echo
  echo "Default rev is @- (the parent of the current change)"
}

for arg in "$@"; do
  case "$arg" in
    -h|--help)
      usage
      exit 0
      ;;
  esac
done


local rev="$1"
if [[ -z $rev ]]; then
  rev="@-"
fi
local newname=$(jj log -r "$rev" --no-graph -n1 -T 'description.first_line()' \
  | sed -Ee 's/[^a-zA-Z0-9]+/-/g' \
  | sed -Ee 's/^-+|-+$//g' \
  | tr '[:upper:]' '[:lower:]' \
  ;
)
if [[ -z $newname ]]; then
  echo "bookmark at $rev has no description"
  return 1
fi
local mark="bwm/$newname"
jj bookmark create "$mark" -r "$rev"

local cmd=(jj git push --allow-new -b $mark)
echo "> ${cmd[*]}"
local outfile=$(mktemp --tmpdir jjab.out.XXXXXXXXXX)
${cmd[@]} 2>&1 | tee "$outfile"
local pr_url=$(grep -Eo 'https://github.com/.*pull/new[^ ]*' "$outfile")
rm "$outfile"
if [[ -n "$pr_url" ]]; then
  python3 -m webbrowser "$pr_url"
else
  echo "jjab: No PR URL found"
fi
